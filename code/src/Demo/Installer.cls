Include %occInclude

Class Demo.Installer
{

Parameter NAMESPACE As STRING = "FHIR";

XData Install [ XMLNamespace = INSTALLER ]
{
<Manifest>
    <Log Text="UnExpire Passwords..." Level="0"/>
	<Invoke Class="Demo.Installer" Method="UnExpireUserPasswords" CheckStatus="true">
	</Invoke>
    <Log Text="Creating ${NAMESPACE} namespace and DB..." Level="0"/>
    <Default Name="RESOURCE" Value="%DB_${NAMESPACE}" />
    <Namespace Name="${NAMESPACE}" Code="${NAMESPACE}" Data="${NAMESPACE}" Create="yes" Ensemble="0">
        <Configuration>
            <Database Name="${NAMESPACE}" Dir="${MGRDIR}${DBNAME}" Create="yes" MountAtStartup="true"/>
        </Configuration>
        <Log Text="Enable interoperability to ${NAMESPACE}..." Level="0"/>
	    <Invoke Class="%Library.EnsembleMgr" Method="EnableNamespace" CheckStatus="true">
            <Arg name="pTargetNS" Value="${NAMESPACE}"/>
            <Arg name="pVerbose" Value="1"/>
	    </Invoke>
        <IfDef Var="SOURCE">
            <Log Text="SOURCE defined - offline install from ${SOURCE}" Level="0"/>
            <Import File="${SOURCE}" Recurse="true"/>
        </IfDef>
        <Log Text="Install FHIR..." Level="0"/>
        <Invoke Class="Demo.Installer" Method="InstallFHIR" CheckStatus="true">
        </Invoke>
    </Namespace>
</Manifest>
}

/// This is a method generator whose code is generated by XGL.
ClassMethod Installer(ByRef pVars, pLogLevel As %Integer = 3, pInstaller As %Installer.Installer, pLogger As %Installer.AbstractLogger) As %Status [ CodeMode = objectgenerator, Internal ]
{
    return ##class(%Installer.Manifest).%Generate(%compiledclass,%code,"Install")
}

ClassMethod UnExpireUserPasswords() As %Status
{
    set sc = $$$OK
    try {
        new $namespace
        set $namespace = "%SYS"
        do ##class(Security.Users).UnExpireUserPasswords("*")
    } catch (err) {
        set sc = $$$ADDSC(sc,err.AsStatus())
    }
    return sc
}

ClassMethod Install(path As %String) As %Status
{
    set sc = $$$OK
    try {
        set args("NAMESPACE") = ..#NAMESPACE
        set args("DBNAME") = $zconvert(..#NAMESPACE,"L")
        set args("SOURCE") = path
        set args("PRODUCTION") = "Demo.Production"
        
        #; install manifest
        set sc = $$$ADDSC(sc,..Installer(.args))
    } catch (err) {
        set sc = $$$ADDSC(sc,err.AsStatus())
    }
    return sc
}

ClassMethod InstallFHIR() As %Status
{
    set sc = $$$OK
    try {
        set appKey = "/demo/fhir/r4"
        set strategyClass = "HS.FHIRServer.Storage.Json.InteractionsStrategy"
        set metadataConfigKey = "HL7v40"

        //Install a Foundation namespace and change to it
        do ##class(HS.HC.Util.Installer).InstallFoundation("FHIR")
        set $namespace = "FHIR"

        // Install elements that are required for a FHIR-enabled namespace
        do ##class(HS.FHIRServer.Installer).InstallNamespace()

        // Install an instance of a FHIR Service into the current namespace
        do ##class(HS.FHIRServer.Installer).InstallInstance(appKey, strategyClass, metadataConfigKey)
    } catch (err) {
        set sc = $$$ADDSC(sc,err.AsStatus())
    }
    return sc
}

}
